if(isRunMy.toBoolean() == true) //如果"我的"模块独立运行
{
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        if(isRunMy.toBoolean() == true) //如果"我的"模块独立运行
        {
            applicationId "com.mvp.gch.my"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //这个javaCompileOptions的代码所有moudle都要添加，为的是做区分
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
            annotationProcessorOptions { includeCompileClasspath = true } //处理butterknife的依赖问题
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets{
        main{
            if(isRunMy.toBoolean() == true) //如果"我的"模块要单独运行
            {
                manifest.srcFile 'src/main/single_run/AndroidManifest.xml'
            }else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

}

dependencies {

    implementation project(':base')


    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    //这里要加这行代码,否则单独运行时不能识别R2
    annotationProcessor "com.jakewharton:butterknife-compiler:9.0.0-rc1"
}
